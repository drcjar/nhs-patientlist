jQuery ($) ->
  $('#new_own_patient_list').bind
    'ajax:before': (event) ->
      $('input[type="submit"]', event.target).attr 'disabled', 'true'
      #disable submit button
    'ajax:success': (event, data) ->
      $('.error', event.target).removeClass 'error'
      $('#list-messages').html ''
      $("""<li><a href="/users/#{data['user_id']}/patient_lists/#{data['id']}" data-patient-list-id="#{data['id']}">#{data['name']}</a></li>""").appendTo '#patient-list-index'
    'ajax:error': (event, data) ->
      $('#list-messages').text JSON.parse(data['responseText'])['name']
      $('#new-list-name', event.target).addClass 'error'
    'ajax:complete': (event) ->
      $('input[type="submit"]', event.target).removeAttr 'disabled'

  drop = (event, ui) ->
    patientListId = $(event.target).attr 'data-patient-list-id'
    patientId     = $(ui.helper).attr 'data-patient-id'
    $.ajax
      url: "<%= Rails.application.routes.url_helpers.memberships_path %>"
      type: 'post'
      dataType: 'json'
      contentType: 'application/json'
      data:
        JSON.stringify
          membership:
            patient_list: patientListId
          patient_id:   patientId
      error: (json) ->
        if json.responseText.match /already/
          alert "Patient is already on the list"
        else
          alert "Could not add patient to list"
  
  divhelper = (event) ->
      $target = $(event.target).closest('tr')
      $("""<div class="dragging" data-patient-id="#{$target.attr('data-patient-id')}"></div>""")
        .append($target.find('td:eq(3)').clone())
        .insertAfter('#main table')

  $('#main table tr').draggable
    helper: divhelper
    distance: 5
    cursor: 'move'
    cursorAt:
      top: -5
      left: -5
    revert: 'invalid'

  $('#sidebar ul li a').droppable
    tolerance: 'pointer'
    hoverClass: 'drop-hover'
    drop: drop
